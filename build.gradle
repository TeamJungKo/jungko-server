plugins {
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
}

group = 'com.jungko'
version = '0.0.1-SNAPSHOT'

java.sourceCompatibility = '17'

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'local'
}

repositories {
    mavenCentral()
}

dependencies {
//  Spring Boot
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0')
    implementation('org.springframework.boot:spring-boot-starter')

// Monitoring
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('io.micrometer:micrometer-registry-prometheus')
    implementation('io.micrometer:micrometer-core')

//  Database
    runtimeOnly('org.mariadb.jdbc:mariadb-java-client')

//  Lombok
    compileOnly('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')

//  Development
    developmentOnly('org.springframework.boot:spring-boot-devtools')
    annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')

//  Test
    testCompileOnly('org.projectlombok:lombok')
    testAnnotationProcessor('org.projectlombok:lombok')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

tasks.register('copyMainConfig', Copy) {
    from 'server-config/main/resources'
    into 'src/main/resources'
}

tasks.register('copyTestConfig', Copy) {
    from 'server-config/test/resources'
    into 'src/test/resources'
}

tasks.named('processResources') {
    dependsOn 'copyMainConfig'
}

tasks.named('processTestResources') {
    dependsOn 'copyTestConfig'
}

tasks.withType(Test) {
    useJUnitPlatform()
}
